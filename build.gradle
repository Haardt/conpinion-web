buildscript {
  repositories {
//    maven { url nexusURL }
// public alternatives for the nexus connection:
    jcenter()
    mavenLocal() // for git-changelog
    maven { url 'https://plugins.gradle.org/m2/' } // for net.ltgt.apt
  }
  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
    classpath 'pl.allegro.tech.build:axion-release-plugin:1.3.4'
    classpath 'net.ltgt.gradle:gradle-apt-plugin:0.6'
  }
}

plugins {

  id 'com.selesse.git.changelog' version "0.2.0"
  id 'com.github.ben-manes.versions' version '0.13.0'

}

task wrapper(type: Wrapper) { gradleVersion = '2.14.1' }
apply plugin: 'com.github.ben-manes.versions'
dependencyUpdates { revision = 'release' }


//
// version and changelog management

//apply plugin: 'changelog'
apply plugin: 'pl.allegro.tech.build.axion-release'
//changelog { versionNum 'changelog_version' }
scmVersion {
  localOnly = false
  tag { prefix = '' }
  checks { uncommittedChanges = false }
  hooks {
    pre 'fileUpdate', [file: 'CHANGELOG.md', pattern: { v, p -> /changelog_version/ }, replacement: { v, p -> "${v}" }]
    pre 'fileUpdate', [file: 'version', pattern: { v, p -> /${v}/ }, replacement: { v, p -> "${v}" }]
    pre 'commit', { v, p -> "chore: bump version to ${v}" }
  }
}
createRelease.dependsOn 'generateChangelog'
release.dependsOn 'generateChangelog'
task bump(dependsOn: createRelease)

//
// global properties
changelog {
  // The title appears at the top of the changelog.
  // Default value: the name of the project.
  title = "${project.name} - Changelog"

  // The output directory where the report is generated.
  // Default value: main resource directory, or the "build" directory
  outputDirectory = file("$projectDir")
}

allprojects {
  group = 'de.conpinion.web'
  project.version = scmVersion.version

  ext.jacocoVersion = '0.7.4.201502262128'

  repositories {
//    maven { url nexusURL }
// public alternatives for the nexus connection:
    mavenLocal() // for javax.xml.namespace and javax.xml.jax-qname
    mavenCentral()
    jcenter()
  }
}


subprojects {
  apply plugin: 'java'
  apply plugin: 'groovy'
  apply plugin: 'idea'
  apply plugin: 'net.ltgt.apt'
  apply from: "${rootProject.projectDir}/gradle/libraries.gradle"
  apply from: "${rootProject.projectDir}/gradle/global_dependencies.gradle"
  apply from: "${rootProject.projectDir}/gradle/jacoco_module.gradle"
  apply from: "${rootProject.projectDir}/gradle/artifacts.gradle"

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  tasks.withType(JavaCompile) { options.incremental = true }
  compileJava.options*.compilerArgs = ['-Xlint:deprecation', '-Xlint:unchecked']
  compileTestJava.options*.compilerArgs = ['-Xlint:deprecation']

  tasks.withType(GroovyCompile) { groovyOptions.javaAnnotationProcessing = true }
  tasks.withType(GroovyCompile) { groovyOptions.optimizationOptions.indy = true }
  compileGroovy.options*.compilerArgs = ['-Xlint:deprecation', '-implicit:class']
  compileTestGroovy.options*.compilerArgs = ['-Xlint:deprecation', '-implicit:class']
}

//
// additional task and tweaks
apply from: "${rootProject.projectDir}/gradle/jacoco_root.gradle"
